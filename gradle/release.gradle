
ext.exec = { command ->
    def output = new StringBuilder()
    def error = new StringBuilder()

    def process = command.execute()

    process.consumeProcessOutput(output, error)

    def ret = process.waitFor()
    if(ret) {
        throw new RuntimeException("Failed to execute command '${command}': ${error}")
    }

    return output.toString()
}


ext.getVersionName = {
    def tagname = getTagName()
    if(getCurrentBranch() != "master") {
        return tagname + "-SNAPSHOT"
    } else {
        return tagname
    }
}

ext.getCurrentBranch = {
    def branch = System.getenv("TRAVIS_BRANCH")
    if (!branch)
      branch = exec('git rev-parse --abbrev-ref HEAD').trim()
    if (branch == 'HEAD')
      branch = exec('git log -n 1 --pretty=%D HEAD').trim().split(' ').last()
    return branch
}

ext.getTagName = {
    try {
        return exec('git describe --tags').trim()
    }
    catch (ignored) {
        return 'untagged'
    }
}

version = getVersionName()
